#!/usr/bin/gnuplot
#
# Script        :cpu.gplot
# Author        :Julio Sanz
# Website       :www.elarraydejota.com
# Email         :juliojosesb@gmail.com
# Description   :Script to create a CPU usage graph with gnuplot
# Dependencies  :gnuplot
# Usage         :gnuplot cpu.gplot
# License       :GPLv3
#

reset

# Graph terminal and general config
set terminal pngcairo enhanced font 'Verdana,8'
set output '../../graphs/cpu.png'
set title "CPU usage graph"
set key bmargin

# Styles for different lines
set style line 1 lc rgb '#e74c3c' pt 1 ps 1 lt 1 lw 2 # line1
set style line 2 lc rgb '#3498db' pt 6 ps 1 lt 1 lw 2 # line2
set style line 3 lc rgb '#28b463' pt 4 ps 1 lt 1 lw 2 # line3

# Axis configuration
set style line 11 lc rgb '#2c3e50' lt 1 lw 1.5 # Axis line
set border 3 back ls 11
set tics nomirror
set autoscale xy
set xdata time
set timefmt "%H:%M:%S"
set xlabel "Time"
set ylabel "CPU %"

# Background grid
set style line 11 lc rgb '#aeb6bf' lt 0 lw 2
set grid back ls 11

# Begin plotting
set xdata
stats '../../data/cpu.dat' using 3 name "USER"
stats '../../data/cpu.dat' using 5 name "SYS"
stats '../../data/cpu.dat' using 8 name "IDLE"
set xdata time

plot '../../data/cpu.dat' using 1:3 title sprintf("User %0.2f",USER_mean) with l ls 1, \
     ''                   using 1:5 title sprintf("System %0.2f",SYS_mean) with l ls 2, \
     ''                   using 1:8 title sprintf("Idle %0.2f",IDLE_mean) with l ls 3
