#!/usr/bin/gnuplot
#
# Script        :loadaverage.gplot
# Author        :Julio Sanz
# Website       :www.elarraydejota.com
# Email         :juliojosesb@gmail.com
# Description   :Script to create a LoadAverage graph with gnuplot
# Dependencies  :gnuplot
# Usage         :gnuplot loadaverage.gplot
# License       :GPLv3
#

reset

set terminal pngcairo enhanced font 'Verdana,8'
set output '../../graphs/loadaverage.png'
set title "System load average graph"
set key bmargin

# Styles for different lines
set style line 1 lc rgb '#e74c3c' pt 1 ps 1 lt 1 lw 2 # line1
set style line 2 lc rgb '#3498db' pt 6 ps 1 lt 1 lw 2 # line2
set style line 3 lc rgb '#28b463' pt 4 ps 1 lt 1 lw 2 # line3

# Axis configuration
set style line 11 lc rgb '#2c3e50' lt 1 lw 1.5 # Axis line
set border 3 back ls 11
set tics nomirror
set autoscale xy
set xdata time
set timefmt "%H:%M:%S"
set xlabel "Time"
set ylabel "Load average"

# Background grid
set style line 11 lc rgb '#aeb6bf' lt 0 lw 2
set grid back ls 11

# Begin plotting
set xdata
stats '../../data/loadaverage.dat' using 4 name "AVGONE"
stats '../../data/loadaverage.dat' using 5 name "AVGFIVE"
stats '../../data/loadaverage.dat' using 6 name "AVGFIFT"
set xdata time

plot '../../data/loadaverage.dat' using 1:4 title sprintf("1 min %0.2f",AVGONE_mean) with l ls 1, \
     ''                           using 1:5 title sprintf("5 min %0.2f",AVGFIVE_mean) with l ls 2, \
     ''                           using 1:6 title sprintf("15 min %0.2f",AVGFIFT_mean) with l ls 3
